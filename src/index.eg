require:
   "@cycle/core" -> {run, Rx}
   "@cycle/dom" -> {h, makeDOMDriver}

let Observable = Rx.Observable

egh = match ->
   String? s -> s
   ENode? {tags => {}, => props, => children} and enode -> enode.to-string()
   ENode? {=> tags, => props, => children} ->
      h(tags.join(""), props) with
         children each child -> egh(child)

;; ================ App ==================
footer = responses -> {
   DOM = Observable.return:
      egh: div % "=== footer ==="
}

intent = {=> DOM} -> {
   first-name$ = chain DOM:
      @get("#first-name", .input)
      @map: event -> event.target.value
   last-name$ = chain DOM:
      @get("#last-name",  .input)
      @map: event -> event.target.value
}
model = actions -> {
   first-name$ = chain actions:
      @first-name$
      @start-with("")
   last-name$  = chain actions:
      @last-name$
      @start-with("")
}
view = state -> {
   DOM = Observable.combine-latest with
      state.first-name$
      state.last-name$
      (first-name, last-name) ->
         egh:
            div %
               div.form-group %
                  label %
                     attributes = { html-for = "first-name" }
                     "First Name:"
                  input.form-control %
                     attributes = {
                        type = .text
                        id = "first-name"
                        placeholder = "First Name"
                     }
               div.form-group %
                  label %
                     attributes = { html-for = "last-name" }
                     "Last Name:"
                  input.form-control %
                     attributes = {
                        type = .text
                        id = "last-name"
                        placeholder = "Last Name"
                     }
               hr %
               h3 % 'Hello {first-name} {last-name}'
               app-footer %
                  attributes = { key = .footer }
}

main = responses -> view(model(intent(responses)))

run main:
   {
      DOM = makeDOMDriver "#app": {
         "app-footer" => footer
      }
   }
